########################################################
# AngularJS service to load data import state data in the scope.
########################################################

angular.module('feedbunch').service 'importStateSvc',
['$rootScope', '$timeout', 'feedsFoldersSvc', 'userDataSvc', 'timerFlagSvc',
($rootScope, $timeout, feedsFoldersSvc, userDataSvc, timerFlagSvc)->

  # Flag to indicate if a message should appear when the import job finishes running.
  show_finished_message = false

  # CSRF token
  token = csrfTokenSvc.get_token()

  # Web worker to retrieve state of import job
  worker_load_import_state = new Worker '<%= asset_path 'workers/load_import_state_worker' %>'
  worker_load_import_state.onmessage = (e) ->
    if e.data.status == 200 || e.data.status == 304
      $rootScope.show_import_alert = e.data.response.show_alert
      $rootScope.import_state = e.data.response.state
      if e.data.response.state == "RUNNING"
        # Update state from the server periodically while import is running
        $rootScope.import_processed = e.data.response.import.processed
        $rootScope.import_total = e.data.response.import.total
        $timeout ->
          load_import_state true
        , 5000
      else if e.data.response.state == "ERROR" && show_finished_message
        timerFlagSvc.start 'error_importing'
      else if e.data.response.state == "SUCCESS"
        $rootScope.import_failures = e.data.response.failures
        if show_finished_message
          # Automatically load new feeds and folders without needing a refresh
          feedsFoldersSvc.load_data()
          userDataSvc.load_data()
          timerFlagSvc.start 'success_importing'
    else if e.data.status == 401 || e.data.status == 422
      $window.location.href = '/login'
    else
      timerFlagSvc.start 'error_importing'

  # Web worker to hide import state alert
  worker_hide_import_alert = new Worker '<%= asset_path 'workers/hide_import_alert_worker' %>'

  #---------------------------------------------
  # PRIVATE FUNCTION: load import process state via AJAX
  #
  # Note.- The first time this is invoked on page load by the angular controller, passing
  # a false to the "show_alerts" argument. This means that if the import is not running
  # (error, success or none, doesn't matter), no alert will be shown.
  #
  # However if the import is found to be running, the function will be called every 5 seconds, with
  # a true to the "show_alerts" argument.
  #
  # Basically this means that if when the page is loaded the import is running, and it finishes
  # afterwards, then and only then will an alert be displayed. Also when this happens new feeds and
  # folders will be inserted in the model automatically.
  #---------------------------------------------
  load_import_state = (show_alerts=false)->
    show_finished_message = show_message
    worker_load_import_state.postMessage {token: token}

  service =

    #---------------------------------------------
    # Load import process state via AJAX into the root scope
    #---------------------------------------------
    load_data: load_import_state

    #---------------------------------------------
    # Hide the import state alert and notify the server via AJAX that it should not be displayed again.
    #---------------------------------------------
    hide_alert: ->
      $rootScope.show_import_alert = false
      worker_hide_import_alert.postMessage {token: token}

  return service
]