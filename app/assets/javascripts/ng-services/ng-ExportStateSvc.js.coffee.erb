########################################################
# AngularJS service to load data export state data in the scope.
########################################################

angular.module('feedbunch').service 'exportStateSvc',
['$rootScope', '$http', '$timeout', 'csrfTokenSvc', 'feedsFoldersSvc', 'timerFlagSvc',
($rootScope, $http, $timeout, csrfTokenSvc, feedsFoldersSvc, timerFlagSvc)->

  # Flag to indicate if a message should appear when the export job finishes running.
  show_finished_message = false

  # CSRF token
  token = csrfTokenSvc.get_token()

  # Web worker to retrieve state of export job
  worker = new Worker '<%= asset_path 'workers/load_export_state_worker' %>'
  worker.onmessage = (e) ->
    if e.data.status == 200
      $rootScope.show_export_alert = e.data.response.show_alert
      $rootScope.export_date = e.data.response.export_date
      $rootScope.export_state = e.data.response.state
      if e.data.response.state == "RUNNING"
        # Update state from the server periodically while export is running
        $timeout ->
          load_export_state true
        , 5000
      else if e.data.response.state == "ERROR" && show_finished_message
        timerFlagSvc.start 'error_exporting'
      else if e.data.response.state == "SUCCESS" && show_finished_message
        # Automatically load new feeds and folders without needing a refresh
        feedsFoldersSvc.load_data()
        timerFlagSvc.start 'success_exporting'
    else if e.data.status == 401 || e.data.status == 422
      $window.location.href = '/login'
    else
      timerFlagSvc.start 'error_exporting'

  #---------------------------------------------
  # PRIVATE FUNCTION: load export process state via AJAX
  #
  # Note.- The first time this is invoked on page load by the angular controller, passing
  # a false to the "show_message" argument. This means that if the export is not running
  # (error, success or none, doesn't matter), no message alert will be shown.
  #
  # However if the export is found to be running, the function will be called every 5 seconds, with
  # a true to the "show_message" argument.
  #
  # Basically this means that if when the page is loaded the export is running, and it finishes
  # afterwards, then and only then will a message be displayed.
  #---------------------------------------------
  load_export_state = (show_message=false)->
    show_finished_message = show_message
    worker.postMessage {token: token, show_message: show_message}

  service =

    #---------------------------------------------
    # Load export process state via AJAX into the root scope
    #---------------------------------------------
    load_data: load_export_state

    #---------------------------------------------
    # Hide the export state alert and notify the server via AJAX that it should not be displayed again.
    #---------------------------------------------
    hide_alert: ->
      $rootScope.show_export_alert = false
      $http.put("/api/opml_exports.json", opml_export: {show_alert: 'false'})

  return service
]